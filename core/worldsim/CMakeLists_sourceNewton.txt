# Script to compile the salsa worldsim library (with compilation of Newton Game
# Dynamics)

# Including the directories with Newton Game Dynamics and QGLViewer
add_subdirectory(3rdParts/newton)
add_subdirectory(3rdParts/qglviewer)

# Adding OpenGL
find_package(OpenGL REQUIRED)

set(SALSAWORLDSIM_SRCS
	src/salsaglutils.cpp
	src/glcontextanddata.cpp
	src/graphicalmarkers.cpp
	src/guirendererscontainer.cpp
	src/libinitializer.cpp
	src/motorcontrollers.cpp
	src/ownerfollower.cpp
	src/phyballandsocket.cpp
	src/phybox.cpp
	src/phycompoundobject.cpp
	src/phycone.cpp
	src/phycylinder.cpp
	src/phyellipsoid.cpp
	src/phyepuck.cpp
	src/phyfixed.cpp
	src/phyheightfield.cpp
	src/phyhinge.cpp
	src/phyjoint.cpp
	src/phykhepera.cpp
	src/phymarxbot.cpp
	src/phyobject.cpp
	src/physlider.cpp
	src/physphere.cpp
	src/physuspension.cpp
	src/phyuniversal.cpp
	src/rendererscontainer.cpp
	src/renderworld.cpp
	src/sensorcontrollers.cpp
	src/singleir.cpp
	src/wentity.cpp
	src/wmesh.cpp
	src/wobject.cpp
	src/world.cpp
	src/worldhelpers.cpp
	src/worldsimutils.cpp)
set(SALSAWORLDSIM_HDRS
	include/salsaglutils.h
	include/glcontextanddata.h
	include/graphicalmarkers.h
	include/guirendererscontainer.h
	include/motorcontrollers.h
	include/ownerfollower.h
	include/phyballandsocket.h
	include/phybox.h
	include/phycompoundobject.h
	include/phycone.h
	include/phycylinder.h
	include/phyellipsoid.h
	include/phyepuck.h
	include/phyfixed.h
	include/phyheightfield.h
	include/phyhinge.h
	include/phyjoint.h
	include/phykhepera.h
	include/phymarxbot.h
	include/phyobject.h
	include/physlider.h
	include/physphere.h
	include/physuspension.h
	include/phyuniversal.h
	include/private/phyjointprivate.h
	include/private/phyobjectprivate.h
	include/private/worldprivate.h
	include/rendererscontainer.h
	include/renderingproxy.h
	include/renderworld.h
	include/sensorcontrollers.h
	include/singleir.h
	include/wentity.h
	include/wmatrix.h
	include/wmesh.h
	include/wobject.h
	include/world.h
	include/worldhelpers.h
	include/worldsimconfig.h
	include/worldsimexceptions.h
	include/worldsimutils.h
	include/wquaternion.h
	include/wvector.h)

# Adding resources and ui files
qt5_add_resources(SALSAWORLDSIM_SRCS textures/textures.qrc)

add_library(salsaworldsim SHARED ${SALSAWORLDSIM_SRCS} ${SALSAWORLDSIM_HDRS})
add_salsa_version(salsaworldsim)

# Specifying the the include directories: they are used both here and  exported
# by this library (so that targets linking this one will automatically import
# the include directories declared here). CMAKE_SOURCE_DIR is needed for
# salsaversion.h
target_include_directories(salsaworldsim PUBLIC
                           $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
                           $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/3rdParts>
                           $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/3rdParts/newton>
                           $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}>
                           $<INSTALL_INTERFACE:include/salsa/worldsim>
                           $<INSTALL_INTERFACE:include/salsa>)

# Adding dependencies (they are also exported, so targets linking this one will
# automatically link libraries declared here)
target_link_libraries(salsaworldsim salsaconfiguration salsautilities salsanewton salsaqglviewer Qt5::Core Qt5::Xml Qt5::Concurrent Qt5::Widgets Qt5::OpenGL ${OPENGL_LIBRARIES})

# Defines to use. The first ones are used only when compiling the library, the
# second one both when compiling the library and targets using the library
target_compile_definitions(salsaworldsim PRIVATE SALSA_WSIM_BUILDING_DLL)
target_compile_definitions(salsaworldsim PUBLIC WORLDSIM_USE_NEWTON)

# Specifying the public headers of this target
set_property(TARGET salsaworldsim PROPERTY PUBLIC_HEADER ${SALSAWORLDSIM_HDRS})

# Installation paths
install(TARGETS salsaworldsim
        EXPORT salsa
        ARCHIVE DESTINATION lib/
        LIBRARY DESTINATION lib/
        PUBLIC_HEADER DESTINATION include/salsa/worldsim
        RUNTIME DESTINATION bin/)
